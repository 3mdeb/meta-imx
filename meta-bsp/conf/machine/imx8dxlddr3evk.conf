#@TYPE: Machine
#@NAME: i.MX 8DXL DDR3 EVK Board
#@DESCRIPTION: Machine configuration for NXP i.MX 8DXL DDR3 Evaluation Kit (EVK) Board
#@MAINTAINER: Carol Zhu <carol.zhu@nxp.com>

MACHINEOVERRIDES =. "mx8:mx8x:mx8dxl:"

require conf/machine/include/imx-base.inc
require conf/machine/include/arm/arch-arm64.inc

# Disable optee feature temporarily
#MACHINE_FEATURES_append = " optee bcm43455 bcm4356 bcm4359 nxp8997"
MACHINE_FEATURES_append = " bcm43455 bcm4356 bcm4359 nxp8997"

# Don't include kernels in standard images
RDEPENDS_${KERNEL_PACKAGE_NAME}-base = ""
RDEPENDS_${KERNEL_PACKAGE_NAME}-image = ""

LOADADDR = ""

# Disable the serial console due to auto-serial-console
SERIAL_CONSOLES = "115200;ttyLP0"

# we do not want to have getty running on tty1 as we run
# auto-serial-console there
USE_VT = "0"

KERNEL_DEVICETREE = "freescale/imx8dxl-ddr3-evk.dtb \
                     freescale/imx8dxl-ddr3-evk-rpmsg.dtb \
"

UBOOT_MAKE_TARGET = "all"
SPL_BINARY = "spl/u-boot-spl.bin"

UBOOT_CONFIG ??= "sd"
UBOOT_CONFIG[sd]   = "imx8dxl_ddr3_evk_defconfig,sdcard"
UBOOT_CONFIG[nand]   = "imx8dxl_ddr3_evk_nand_defconfig"

IMAGE_BOOTLOADER = "imx-boot"
IMX_BOOT_SEEK = "32"

IMXBOOT_TARGETS_SD = \
    "${@bb.utils.contains('MACHINE_FEATURES', 'optee', 'flash_spl', \
                                                       'flash_regression_linux_m4 flash', d)}"
IMXBOOT_TARGETS_NAND = \
    "${@bb.utils.contains('MACHINE_FEATURES', 'optee', 'flash_spl_nand', \
                                                       'flash_nand flash_nand_fw', d)}"
IMXBOOT_TARGETS = \
    "${@bb.utils.contains('UBOOT_CONFIG',        'sd', '${IMXBOOT_TARGETS_SD}', \
                                                       '${IMXBOOT_TARGETS_NAND}', d)}"

BOARD_TYPE = "val"
